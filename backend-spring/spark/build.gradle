plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.18'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'com.github.johnrengelman.shadow' version '7.1.2'

}

group = 'com.ssafy'
version = '0.0.1-SNAPSHOT'
description = 'spark'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}


dependencies {

	// Spring Boot Starter (기본)
	implementation 'org.springframework.boot:spring-boot-starter-web'         // Spring MVC (REST API)
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'    // JPA + Hibernate
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'// MongoDB
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'   // Thymeleaf Template Engine
	implementation 'org.springframework.boot:spring-boot-starter-validation'  // Bean Validation(JSR-380)

	// DB Drivers
	runtimeOnly 'com.mysql:mysql-connector-j'   // MySQL

	// Lombok
	compileOnly 'org.projectlombok:lombok'      // Lombok (컴파일 시에만 필요)
	annotationProcessor 'org.projectlombok:lombok'

	// Swagger (Springdoc OpenAPI)
	implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'



	// 개발 편의 기능
	developmentOnly 'org.springframework.boot:spring-boot-devtools' // Hot reload 지원
	developmentOnly 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.12.2'

	// AWS SDK
	implementation 'com.amazonaws:aws-java-sdk-s3:1.12.565'

	// Spring Cloud AWS  (AWS SDK 포함됨)
	// implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

	// Spark용 javax.servlet 의존성 추가
	implementation 'javax.servlet:javax.servlet-api:4.0.1'

	// Apache Spark 관련 (로깅 충돌 방지)
	implementation('org.apache.spark:spark-core_2.13:3.4.3') {
		exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		exclude group: 'log4j', module: 'log4j'
		exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
	}
	implementation('org.apache.spark:spark-sql_2.13:3.4.3') {
		exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		exclude group: 'log4j', module: 'log4j'
		exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
	}
	implementation('org.apache.spark:spark-hadoop-cloud_2.13:3.4.3') {
		exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		exclude group: 'log4j', module: 'log4j'
		exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
	}

	// Hadoop AWS (S3 연동용) - 로깅 충돌 방지
	implementation('org.apache.hadoop:hadoop-aws:3.3.4') {
		exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		exclude group: 'log4j', module: 'log4j'
		exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
	}

	// 크롤링 관련
	implementation 'org.jsoup:jsoup:1.16.1'
	implementation 'org.seleniumhq.selenium:selenium-java:4.15.0'

	// 스케줄링
	implementation 'org.springframework.boot:spring-boot-starter-quartz'

	// WebFlux 전체 대신 WebClient만 사용
	implementation 'org.springframework:spring-webflux'
	implementation 'io.projectreactor.netty:reactor-netty-http'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'io.github.cdimascio:java-dotenv:5.1.1'

	// 버전 충돌 방지를 위한 강제 지정
	configurations.all {
		resolutionStrategy {
			force 'org.apache.spark:spark-core_2.13:3.4.3'
			force 'org.apache.spark:spark-sql_2.13:3.4.3'
		}
	}
}


tasks.named('test') {
	useJUnitPlatform()
}
// Shadow JAR 설정
shadowJar {
	archiveBaseName.set('spark-app')
	archiveClassifier.set('')

	manifest {
		attributes 'Main-Class': 'com.ssafy.spark.SparkApplication'  // 실제 메인클래스명으로 변경!
	}

	mergeServiceFiles()

	exclude 'META-INF/*.RSA'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
}